varnishtest "Add X-Authcache-Do-ESI header when no session is open and key retrieval is enabled"

server s1 {
        rxreq
        expect req.url == "/authcache-varnish-get-key"
        expect req.http.X-Forwarded-For == "127.0.0.1"
        txresp -hdr "X-Authcache-Key: 123"

        rxreq
        expect req.url == "/"
        expect req.http.X-Authcache-Key == 123
        expect req.http.X-Authcache-Do-ESI == 1
        expect req.http.X-Forwarded-For == "127.0.0.1"
        txresp
} -start

varnish v1 -vcl+backend {
        ${vcl1}
        ${vcl2}
        ${vcl3}
} -start

client c1 {
        txreq -url "/"
        rxresp
}

client c1 -run
varnish v1 -expect cache_hit == 0

server s1 -wait
varnish v1 -stop
